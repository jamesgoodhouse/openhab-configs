rule "Mood Lights — Turn On at Sunset"
when
  Channel "astro:sun:home_set_with_offset:set#event" triggered START
then
  if (Present.state == ON) {
    logInfo("lights", "Mood Lights — Turning on at Sunset")

    LandingLightbulb_Dimmer.sendCommand(1)
    LivingRoomLightbulb_Dimmer.sendCommand(1)
    LandingUpstairsLightbulb_Dimmer.sendCommand(1)
  }
end

rule "Mood Lights — Turn Off at Midnight"
when
  Time is midnight
then
  logInfo("lights", "Mood Lights — Turning Off")

  LandingLightbulb_Dimmer.sendCommand(0)
  LivingRoomLightbulb_Dimmer.sendCommand(0)
  LandingUpstairsLightbulb_Dimmer.sendCommand(0)
end

rule "Mood Lights — Turn On When Home"
when
  Item Present changed to ON
then
  val sunsetTimeWithOffset = SunsetTimeWithOffset.getStateAs(DateTimeType).zonedDateTime

  if (now.isEqual(sunsetTimeWithOffset) || (now.isAfter(sunsetTimeWithOffset))) {
    logInfo("lights", "Mood Lights — Turning Back On")

    LandingLightbulb_Dimmer.sendCommand(1)
    LivingRoomLightbulb_Dimmer.sendCommand(1)
    LandingUpstairsLightbulb_Dimmer.sendCommand(1)
  }
end
