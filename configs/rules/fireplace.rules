rule "Fireplace — Thermostat"
when
  Item HVAC_IndoorUnit_LivingRoom_Temperature changed or
  Item Fireplace_Thermostat_Power changed to ON
then
  val hysteresis = (Fireplace_Thermostat_Hysteresis.state as QuantityType<Temperature>).intValue
  val setpoint = (Fireplace_Thermostat_SetPoint.state as QuantityType<Temperature>).intValue
  val temp = (HVAC_IndoorUnit_LivingRoom_Temperature.state as QuantityType<Temperature>).intValue

  if (temp < setpoint - hysteresis) {
    Fireplace_Power.sendCommand(ON)
  } else if (temp >= setpoint) {
    Fireplace_Power.sendCommand(OFF)
  }
end


// sync power state with what the fireplace is saying
rule "Fireplace — Sync Power State"
when
  Item Fireplace_State changed to OFF
then
  if (Fireplace_Power.state == ON) {
    Fireplace_Power.sendCommand(OFF)
  }
end

// ensure it actually was turned on, otherwise set power to OFF
rule "Fireplace — Check Status After Turning On"
when
  Item Fireplace_Power changed to ON
then
  createTimer(now.plusSeconds(2), [ |
    if (Fireplace_State.state == OFF) {
      Fireplace_Power.sendCommand(OFF)
    }
  ])
end
