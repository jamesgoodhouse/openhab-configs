import java.util.ArrayList
import java.util.List

rule "Doors Left Open"
when
  Item Present changed to OFF
then
  val List<String> openDoors = new ArrayList<String>()

  if (Sensor_Binary_DeckDoor_State.state == OPEN)        openDoors.add("- *Deck Door*")
  if (Sensor_Binary_FrontDoor_State.state == OPEN)       openDoors.add("- *Front Door*")
  if (Sensor_Tilt_GarageDoor_State.state == OPEN)        openDoors.add("- *Garage Door*")
  if (Sensor_Binary_GarageEntryDoor_State.state == OPEN) openDoors.add("- *Garage Entry Door*")

  if (openDoors.size() > 0) {
    val telegramAction = getActions("telegram","telegram:telegramBot:boty_mcbotface")
    telegramAction.sendTelegram("⚠️ One or more doors were left open: \n" + String.join("\n", openDoors))
  }
end
