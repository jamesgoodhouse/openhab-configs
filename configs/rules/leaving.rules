import java.util.ArrayList
import java.util.List

rule "Leaving"
when
  Item PresentWithDelay_30 changed to OFF
then
  // CHECK FOR OPEN DOORS ------------------------------------------------------
  val List<String> openDoors = new ArrayList<String>()

  if (DeckDoor.state   == OPEN) openDoors.add("Deck Door")
  if (FrontDoor.state  == OPEN) openDoors.add("Front Door")
  if (GarageDoor.state == OPEN) openDoors.add("Garage Door")

  if (openDoors.size() > 0) {
    val telegramAction = getActions("telegram","telegram:telegramBot:boty_mcbotface")
    telegramAction.sendTelegram("⚠️ One or more doors were left open: \n\n*" + String.join("\n", openDoors) + "*")
  }

  // TURN OFF HVAC -------------------------------------------------------------
  if (FireplaceThermostat_Power.state == ON && FireplaceThermostat_KeepOnWhenLeaving.state != ON) {
    logInfo("fireplace", "Fireplace — Turning Off")
    FireplaceThermostat_Power.sendCommand(OFF)
  }
  if (LivingRoomHVAC_Power.state == ON && LivingRoomHVAC_KeepOnWhenLeaving.state != ON) {
    logInfo("hvac", "Living Room HVAC — Turning Off")
    LivingRoomHVAC_Power.sendCommand(OFF)
  }
  if (MainBedroomHVAC_Power.state == ON && MainBedroomHVAC_KeepOnWhenLeaving.state != ON) {
    logInfo("hvac", "Main Bedroom HVAC — Turning Off")
    MainBedroomHVAC_Power.sendCommand(OFF)
  }

  // TURN OFF LIGHTS -----------------------------------------------------------
  if (GarageOverheadLights_Switch.state == ON) GarageOverheadLights.sendCommand(OFF)
end
