import java.util.Map

rule "External Door Opened"
when
  Member of ExternalDoors changed from CLOSED to OPEN
then
  // External Door Opened While Away
  if (Present.state == OFF) {
    createTimer(now.plusSeconds(60), [ |
      if (Present.state == OFF) {
        if (Rule_NotifyWhenExternalDoorOpenedWhileAway.state == ON) {
          val Map<String, String> nameMap = newHashMap

          nameMap.put("DeckDoor", "Deck Door")
          nameMap.put("FrontDoor", "Front Door")
          nameMap.put("GaragePersonDoor", "Garage Person Door")
          nameMap.put("GarageRollupDoor", "Garage Rollup Door")

          val name = nameMap.getOrDefault(triggeringItem.name, "Unknown")

          logInfo("notification", "Sending Telegram notification")
          val telegramAction = getActions("telegram", "telegram:telegramBot:boty_mcbotface")
          telegramAction.sendTelegram("⚠️ The " + name + " has been opened")
        }
      }
    ])
  }

  // External Door Opened While Home
  if (Present.state == ON && ExternalDoorOpenedSinceHome.state == OFF) ExternalDoorOpenedSinceHome.postUpdate(ON)
end
